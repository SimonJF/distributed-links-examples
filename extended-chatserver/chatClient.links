open ChatSessions


# New architecture:
# DOM handler function, handles messages from both moderator and
# chat messages when joined to the chat room.



# Receives messages from DOM (both standard and moderator).
# Contains outgoing message channels for standard messages
# and also moderator messages.
module OutgoingMessageHandler {

  typename OutgoingMessage =
    [&| ChatMessage: ?Message.End,
        MuteUser: ?Nickname.End,
        KickUser: ?Nickname.End,
        MakeModerator: ?Nickname.End,
        BecomeModerator: ?ModeratorSend.End |&];

  # It's possible for this process to receive mod commands without
  # it actually holding the mod channel. Without the mod channel, it
  # can't relay the message to the server, so it's a no-op.
  # (yes, this is fmap)
  fun withModChannel(f, chan) {
    switch(chan) {
      case Just(chan) -> Just(f(chan))
      case Nothing -> Nothing
    }
  }

  fun init(clientSend, modSend) {
    var (outgoingAP: AP(OutgoingMessage)) = new();

    fun loop(clientSend, modSend) {

     fun isTopicCmd(str) {
        (charAt(str, 0) == '/') &&
        (strlen(str) > 8) &&
        (strsub(str, 0, 7) == "/topic ")
      }

      fun getTopic(str) {
        strsub(str, 7, strlen(str) - 7)
      }

      var s = accept(outgoingAP);
      offer(s) {
        case ChatMessage(s) ->
          var (str, _) = receive(s);
          if (isTopicCmd(str)) {
            var clientSend = select ChangeTopic clientSend;
            var clientSend = send(getTopic(str), clientSend);
            loop(clientSend, modSend)
          } else {
            var clientSend = select ChatMessage clientSend;
            var clientSend = send(str, clientSend);
            loop(clientSend, modSend)
          }
        case MuteUser(s) ->
          var (victim, _) = receive(s);
          var modSend =
            withModChannel(fun(modSend) {
              var modSend = select MuteUser modSend;
              send(victim, modSend)
            }, modSend);
          loop(clientSend, modSend)
        case KickUser(s) ->
          var (victim, _) = receive(s);
          var modSend =
            withModChannel(fun(modSend) {
              var modSend = select KickUser modSend;
              send(victim, modSend)
            }, modSend);
          loop(clientSend, modSend)
        case MakeModerator(s) ->
          var (promotee, _) = receive(s);
          var modSend =
            withModChannel(fun(modSend) {
              var modSend = select MakeModerator modSend;
              send(promotee, modSend)
            }, modSend);
          loop(clientSend, modSend)
        case BecomeModerator(s) ->
          var (newModChan, _) = receive(s);
          # It may be the case that we have an existing moderator
          # channel. If so, cancel it, and replace it.
          var modSend =
            switch(modSend) {
              case Just(modSend) ->
                cancel(modSend);
                Just(newModChan)
              case Nothing -> Just(newModChan)
            };
          # TODO: Setup UI for moderator stuff
          loop(clientSend, modSend)
      }
    }
    ignore(spawnClient { loop(clientSend, modSend) });
    outgoingAP
  }

  fun asynchronously(f) {
    ignore(spawnClient { f() })
  }

  fun chatMessage(outgoingAP, msg) {
    asynchronously(fun() {
      var s = request(outgoingAP);
      var s = select ChatMessage s;
      ignore(send(msg, s))
    })
  }

  fun muteUser(outgoingAP, nick) {
    asynchronously(fun() {
      var s = request(outgoingAP);
      var s = select MuteUser s;
      ignore(send(nick, s))
    })
  }

  fun kickUser(outgoingAP, nick) {
    asynchronously(fun() {
      var s = request(outgoingAP);
      var s = select KickUser s;
      ignore(send(nick, s))
    })
  }

  fun makeModerator(outgoingAP, nick) {
    asynchronously(fun() {
      var s = request(outgoingAP);
      var s = select MakeModerator s;
      ignore(send(nick, s))
    })
  }

  fun becomeModerator(outgoingAP, modChan) {
    ignore(spawnClient {
      var s = request(outgoingAP);
      var s = select BecomeModerator s;
      ignore(send(modChan, s))
    })
  }

}

module DomFunctions {
  var nickname_box_id = "name_box";
  var chat_box_id = "chat_box";

  fun getInputContents(dom_id) {
    var textNode = getNodeById(dom_id);
    domGetPropertyFromRef(textNode, "value")
  }

  fun getChatboxContents() {
    getInputContents(chat_box_id)
  }

  fun getNicknameContents() {
    getInputContents(nickname_box_id)
  }


  fun clearInput(dom_id) {
    var textNode = getNodeById(dom_id);
    var _ = domSetPropertyFromRef(textNode, "value", "");
    ()
  }

  fun addToMsgBox(xml) {
    var msgsNode = getNodeById("msgs");
    appendChildren(xml, msgsNode);
    var scrollHeight = domGetPropertyFromRef(msgsNode, "scrollHeight");
    var _ = domSetPropertyFromRef(msgsNode, "scrollTop", scrollHeight);
    ()
  }


  fun addUserToModBox(name) {
      var to_add = <#><option id="/id_placeholder">
                   {stringToXml(name)}</option></#>;
      appendChildren(to_add, getNodeById("clientSelect"));
      var _ = domSetAttributeFromRef(getNodeById("/id_placeholder"),
                                     "value", name);
      domSetAttributeFromRef(getNodeById("/id_placeholder"),
                                     "id", "/moderator"^^name)
  }

  fun removeUserFromModBox(name) {
    removeNode(getNodeById("/moderator" ^^ name));
  }

  fun addChatMsg(nick, msg) {
    var to_add = <#><p><b>{stringToXml(nick)}</b>: {stringToXml(msg)}</p></#>;
    addToMsgBox(to_add)
  }

  fun addNewUser(nick, isMod) {
    var to_add = <#><p><b>{stringToXml(nick)}</b> just joined</p></#>;
    addToMsgBox(to_add);
    if(isMod) {
      addUserToModBox(nick)
    } else { () }
  }

  fun changeTopic(topic) {
    var topicNode = getNodeById("topic");
    var to_add = <#><p><span class="lead"><b>Topic: </b>{stringToXml(topic)}</span></p></#>;
    domReplaceChildren(to_add, topicNode)
  }

  fun removeUser(nick, isMod) {
    var to_add = <#><p><b>{stringToXml(nick)}</b> just left</p></#>;
    addToMsgBox(to_add);
    if(isMod) {
      removeUserFromModBox(nick)
    } else { () }
  }

  fun muteUser(nick) {
    var to_add = <#><p><b>{stringToXml(nick)}</b> got muted :(</p></#>;
    addToMsgBox(to_add)
  }

  fun unmuteUser(nick) {
    var to_add = <#><p><b>{stringToXml(nick)}</b> got unmuted :D </p></#>;
    addToMsgBox(to_add)
  }

  fun addAdminForm(outgoingAP, nicknames) {
    fun getSelectedUser() {
      DomFunctions.getInputContents("clientSelect")
    }

    fun kickUser() {
      if (getSelectedUser() <> "") {
        OutgoingMessageHandler.kickUser(outgoingAP, getSelectedUser())
      } else { () }
    }

    fun muteUser() {
      if (getSelectedUser() <> "") {
        OutgoingMessageHandler.muteUser(outgoingAP, getSelectedUser())
      } else { () }
    }

    fun makeModerator() {
      if (getSelectedUser() <> "") {
        OutgoingMessageHandler.makeModerator(outgoingAP, getSelectedUser())
      } else { () }
    }

    fun makeOptions(names) {
      switch(names) {
        case [] -> ()
        case x::xs -> addUserToModBox(x); makeOptions(xs)
      }
    }


    var adminForm =
      <#>
        <form id="adminForm" l:onsubmit="{()}">
          <p>
            <select id="clientSelect" required="required">
              <option value="">None selected</option>
            </select><br/>
            <input type="submit" id="adminRemove" value="Kick"
            l:onclick="{ kickUser() }"/>
            <input type="submit" id="adminMute" value="Mute/Unmute"
            l:onclick="{ muteUser() }"/>
            <input type="submit" id="adminMakeAdmin" value="Make moderator"
            l:onclick="{ makeModerator() }"/>
          </p>
        </form>
      </#>;
    domReplaceChildren(adminForm, getNodeById("admin_placeholder"));
    makeOptions(nicknames)
  }

}

# Receives messages from the chat server, and modifies the
# DOM as a result.
module IncomingMessageHandler {

  fun init(outgoingAP, clientReceive, isMod, nicks) {
    fun loop(clientReceive, isMod, nicks) {
      offer(clientReceive) {
        case IncomingChatMessage(s) ->
          var ((nick, chatmsg), s) = receive(s);
          debug("Received incoming chat message (" ^^ chatmsg ^^ ") from (" ^^ nick ^^ ")");
          DomFunctions.addChatMsg(nick, chatmsg);
          loop(s, isMod, nicks)
        case NewUser(s) ->
          var (nick, s) = receive(s);
          DomFunctions.addNewUser(nick, isMod);
          loop(s, isMod, nick :: nicks)
        case NewTopic(s) ->
          var (topic, s) = receive(s);
          DomFunctions.changeTopic(topic);
          loop(s, isMod, nicks)
        case UserLeft(s) ->
          var (nick, s) = receive(s);
          DomFunctions.removeUser(nick, isMod); # Poor Nick
          var nicks = filter(fun(nickCmp) { nick <> nickCmp }, nicks);
          loop(s, isMod, nicks)
        case UserMuted(s) ->
          var (nick, s) = receive(s);
          DomFunctions.muteUser(nick);
          loop(s, isMod, nicks)
        case UserUnmuted(s) ->
          var (nick, s) = receive(s);
          DomFunctions.unmuteUser(nick);
          loop(s, isMod, nicks)
        case BecomeModerator(s) ->
          var (chan, s) = receive(s);
          DomFunctions.addAdminForm(outgoingAP, nicks);
          OutgoingMessageHandler.becomeModerator(outgoingAP, chan);
          loop(s, true, nicks)
        case Kick(_) ->
          # FIXME: We want to eliminate the use of redirect.
          redirect("/");
          ()
      }
    }
    ignore(spawnClient { loop(clientReceive, isMod, nicks) })
  }

}


fun mainPage() {
  # loop that receives input from
  # 1. the client (moderator) about removing and muting clients
  # 2. the server about users entering/leaving rooms so they can be removed
  #    from the dropdown selection box
# fun moderatorHandler(channel) {
#   #
#   receive {
#     case NewUser(name) ->
#       makeOption(name);
#       moderatorHandler(channel)
#     case UserLeft(name) ->
#       removeNode(getNodeById("/moderator"^^name));
#       moderatorHandler(channel)
#     case Remove() ->
#       var selectedUser = getInputContents("clientSelect");
#       if (selectedUser == "") {
#         print("No option has been selected");
#         moderatorHandler(channel)
#       } else {
#         var channel = send(Nickname(selectedUser), select RemoveUser channel);
#         moderatorHandler(channel)
#       }
#     case Mute() ->
#       var selectedUser = getInputContents("clientSelect");
#       if (selectedUser == "") {
#         print("No option has been selected");
#         moderatorHandler(channel)
#       } else {
#         var channel = send(Nickname(selectedUser), select MuteUser channel);
#         moderatorHandler(channel)
#       }
#     case MakeModerator() ->
#       var selectedUser = getInputContents("clientSelect");
#       if (selectedUser == "") {
#         print("No option has been selected");
#         moderatorHandler(channel)
#       } else {
#         var channel = send(Nickname(selectedUser),
#                       select MakeModerator channel);
#         moderatorHandler(channel)
#       }
#     case MakeMeModerator(names) ->
#       print("Became a moderator");
#       domReplaceChildren(makeAdminBox(self()),
#                       getNodeById("adminBox_placehodler"));
#       makeOptions(names);
#       moderatorHandler(channel)
#   }
#  }


  fun displayChat(room, topic, nicknames, clientSend, clientReceive, moderatorChan) {

    fun chatBox(outgoingAP) {
      fun submit() {
        OutgoingMessageHandler.chatMessage(outgoingAP, DomFunctions.getInputContents(DomFunctions.chat_box_id));
        DomFunctions.clearInput(DomFunctions.chat_box_id)
      }

      <#>
        <div id="chatBox">
          <div id="admin_placeholder"></div>
          <div id="topic"><p><span class="lead"><b>Room: </b>{stringToXml(room)}</span></p></div>
          <div id="topic"><p><span class="lead"><b>Topic: </b>{stringToXml(topic)}</span></p></div>
          <div id="msgs" style="overflow-y:scroll; width:100%; height:400px;"></div>
          <div>
            <form l:onsubmit="{submit()}">
              <input id="{DomFunctions.chat_box_id}" type="text" style="width:100%"></input>
            </form>
          </div>
        </div>
      </#>
    }

    var (isMod, moderatorChan) =
      switch(moderatorChan) {
        case Just(c) -> (true, Just(c))
        case Nothing -> (false, Nothing)
      };

    # Spawn process which sends messages to the server
    var outgoingAP = OutgoingMessageHandler.init(clientSend, moderatorChan);

    # Spawn process which handles incoming messages from the server
    IncomingMessageHandler.init(outgoingAP, clientReceive, isMod, nicknames);

    # Finally, remove the name request box, put the chatbox in, put mod form in
    # if required, and start off
    domReplaceChildren(chatBox(outgoingAP), getNodeById("main"));

    if (isMod) {
      DomFunctions.addAdminForm(outgoingAP, nicknames)
    } else {
      ()
    }
  }

  fun chooseRoom(ap) {
    var roomFormAP = new();

    fun submitRoom() {
      ignore(request(roomFormAP));
    }

    fun errToString(err) {
      switch(err) {
        case NameInUse -> "That nickname is already in use."
        case BadRoom -> "There was an error connecting to that room."
      }
    }

    fun roomSelectLoop(s, roomCount) {
      fun getRoomName(n) {
        if (n == roomCount) {
          DomFunctions.getInputContents("newRoomName")
        } else {
          var domRef = getNodeById("room" ^^ intToString(n));
          # Stringly-typed, gotta love it
          if (domGetPropertyFromRef(domRef, "checked") == "true") {
            domGetPropertyFromRef(domRef, "value")
          } else {
            getRoomName(n + 1)
          }
        }
      }

      # Wait for the form to be submitted
      ignore(accept(roomFormAP));
      var nickname = DomFunctions.getInputContents("nickname");
      var roomName = getRoomName(0);
      if (roomName == "" || nickname == "") {
        print("Please enter a valid room name and nickname");
        roomSelectLoop(s, roomCount)
      } else {
        var s = send((roomName, nickname), s);
        offer(s) {
          case JoinedOK(s) ->
            var ((topic, nicknames, clientReceive), s) = receive(s);
            displayChat(roomName, topic, nicknames, s, clientReceive, Nothing)
          case JoinedOKAsModerator(s) ->
            var ((topic, nicknames, clientReceive, modSend), s) = receive(s);
            displayChat(roomName,  topic, nicknames, s, clientReceive, Just(modSend))
          case Nope(s) ->
            var (err, _) = receive(s);
            print(errToString(err));
            # Try again.
            chooseRoom(ap)
        }
      }
    }

    fun makeRoomForm(rooms) {
      fun makeRadioRec(rooms, i) {
        switch(rooms) {
          case [] ->
            if (i == 0) {
            <#>
             <div class="radio">
                <label>
                  <input type="radio" name="roomName" id="newRoom" value="newRoom" checked="checked" />
                  New Room
                  <input type="text" id="newRoomName" ></input>
                </label>
              </div>
            </#>
            } else {
            <#>
             <div class="radio">
                <label>
                  <input type="radio" name="roomName"
                    id="newRoom" value="newRoom"  />
                  New Room
                  <input type="text" id="newRoomName" />
                </label>
              </div>
            </#>
            }
          case x::xs ->
            var roomId = "room" ^^ (intToString(i));
            # If radio button is the first, it's checked by default
            var component =
              if (i == 0) {
                <#>
                  <input type="radio" name="roomName"
                    id="{roomId}" value="{x}" checked="checked" />
                </#>
              } else {
                <#>
                  <input type="radio" name="roomName"
                    id="{roomId}" value="{x}" />
                </#>
              };

            <#>
              <div class="radio">
                <label>
                  { component }
                  { stringToXml(x) }
                </label>
              </div>
              { makeRadioRec(xs, i + 1) }
            </#>
        }
      }
      <#>
        <form id="roomForm" l:onsubmit="{ submitRoom() }">
            <div>
              <label for="nickname">
                Nickname
              </label>
              <input type="text" id="nickname" autofocus="true" />
            </div>
            { makeRadioRec(rooms, 0) }
            <div>
              <input type="submit" name="submitButton" value="Join" />
            </div>
        </form>
      </#>
    }

    # At submission, sends the room name and nickname to the server,
    # checks the response from the server, and either shows an error
    # and repeats, or proceeds to the main chat application
    # get all available rooms from the server and pick one via radio buttons

    # First: request from the AP to get a ClientConnect AP
    var (s: ClientConnect) = request(wap);
    # Next, grab the list of rooms
    var (rooms, s) = receive(s);

    # Next, populate the placeholder with the received rooms
    var generated_radio_dom = makeRoomForm(rooms);
    domReplaceChildren(generated_radio_dom, getNodeById("room_placeholder"));

    # Finally, handle room selection
    roomSelectLoop(s, length(rooms));
  }

  fun start() {
    var _ = spawnClient { chooseRoom(wap) };
    page
      <html>
        <head>
          <title>Extended Links Chat Application</title>
          <link href="http://fonts.googleapis.com/css?family=Merriweather:400,300italic,300" rel="stylesheet" type="text/css"></link>
          <link href="http://fonts.googleapis.com/css?family=Kotta+One" rel="stylesheet" type="text/css"></link>
          <link href="http://fonts.googleapis.com/css?family=Lora" rel="stylesheet" type="text/css"></link>
          <link href="css/bootstrap.min.css" rel="stylesheet"></link>
          <link href="css/abcd.css" rel="stylesheet"></link>
        </head>

        <div class="navbar-wrapper">
          <div class="container">
            <div id="navbar" class="navbar navbar-inverse navbar-fixed-top" role="navigation">
            </div>
          </div>
        </div>

        <div class ="jumbotron">
          <div class="container">
            <h1>Links Session-Typed Chat</h1>
            <hr>...complete with totally original stylesheet</hr>
          </div>
        </div>


        <div id="main" class="container marketing">
          <div class="row featurette">
            <div id="roomSelect">
              <p> Choose one of the existing rooms or create a new one.</p>
              <div id="room_placeholder"></div>
            </div>
          </div>
        </div>
      </html>
  }
  start()
}
